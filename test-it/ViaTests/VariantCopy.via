define (PrintVariantWithAttributes dv(.VirtualInstrument (
    Params:c(
        i(Variant inVar)
        i(String inVarName)
    )
    Locals: c(
        e(dv(.Int32 23) myInt)
        e(a(.String *) attribNames)
        e(a(.Variant *) attribValues)
        e(.Variant inVarX)
        e(.Variant inVarY)
        e(dv(.String '') myStrX)
        e(dv(.Int32 0) myIntY)
        e(dv(.ErrorCluster (false 0 '' )) noError)
        e(.ErrorCluster error)
        e(.ErrorCluster error2)
        e(dv(.Int32 0) attribNamesArrayLength)
        e(dv(.Int32 0) attribValuesArrayLength)
    )
    clump (1 // top level
        Copy(noError error)
        Copy(noError error2)

        VariantToData(inVar error myInt)
        GetVariantAttributeAll(inVar attribNames attribValues error)
        ArrayLength(attribNames attribNamesArrayLength)
        ArrayLength(attribValues attribValuesArrayLength)
        Printf("attribNamesArrayLength = %z, attribValuesArrayLength = %z\n" attribNamesArrayLength attribValuesArrayLength)
        BranchIfNE(2 attribNamesArrayLength 0)
        Printf("Get: %s=%z, error=(%z %z), NoAttribs, data=%z\n" inVarName inVar error.status error.code myInt)
        Branch(3)
Perch(2)
        ArrayIndex(attribValues inVarX 0)
        ArrayIndex(attribValues inVarY 1)
        VariantToData(inVarX error2 myStrX)
        VariantToData(inVarY error2 myIntY)
        Printf("Get: %s=%z, error=(%z %z) error2=(%z %z) attribNames=%z values=(%z %z) data=%z\n" inVarName inVar error.status error.code error2.status error2.code attribNames myStrX myIntY myInt)
        Printf("\n")
Perch(3)
    )
)))

define(TestCopyVariantWithAttributes dv(.VirtualInstrument (
    Params:c(
    )
    Locals: c(
        e(.Variant myVar)
        e(dv(.Boolean false) replaced)
        e(dv(.Boolean false) found)
        e(a(.String *) attribNames)
        e(a(.Variant *) attribValues)
        e(dv(.ErrorCluster (false 0 '' )) noError)
        e(.ErrorCluster error)
        e(.Variant myVar2)
    )
    clump (1 // top level
        Printf("TestCopyVariantWithAttributes()\n")
        Copy(myVar myVar2)
        Copy(noError error)
        DataToVariant(245 myVar)

        SetVariantAttribute(myVar "aaa" "xxx" replaced error)
        Printf("Set: myVar=%z, replaced=%z error=(%z %z)\n" myVar replaced error.status error.code)
        SetVariantAttribute(myVar "bbb" 255 replaced error)
        Printf("Set: myVar=%z, replaced=%z error=(%z %z)\n\n" myVar replaced error.status error.code)

        Printf("Before Copy():\n")
        PrintVariantWithAttributes(myVar "myVar")
        Copy(myVar myVar2)
        Printf("After Copy():\n")
        PrintVariantWithAttributes(myVar2 "myVar2")

        DeleteVariantAttribute(myVar * found error)
        Printf("After Delete in myVar():\n")
        PrintVariantWithAttributes(myVar "myVar")
        PrintVariantWithAttributes(myVar2 "myVar2")
    )
)))

enqueue(TestCopyVariantWithAttributes)
