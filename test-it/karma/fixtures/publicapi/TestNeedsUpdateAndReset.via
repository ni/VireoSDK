// Copyright (c) 2020 National Instruments
// SPDX-License-Identifier: MIT

define (MyVI dv(.VirtualInstrument (
    Params: c(
        o(String outputParameter)
        i(String inputParameter)
        io(String inputOutputParameter)
    )
    Locals: c(
        de(.Double localDataItem)
        e(.Double local)
        de(a(Double *) localDataItemArray)
        e(a(Double *) localArray)
        e(dv(a(Double *) (42 43 44)) arrayNewValue)
        de(dv(Boolean true) latchedBoolean)
        e(Boolean latchedBooleanDestination)
    )

    clump(1
        Copy("abc" outputParameter)  // automatically marks value update-needed
        // Cannot copy to an inputParameter
        Copy("def" inputOutputParameter)  // automatically marks value update-needed
        Copy(123.0 localDataItem)  // automatically marks value update-needed
        Copy(456.0 local)  // does NOT automatically mark value update-needed; not a dataItem
        Copy(arrayNewValue localDataItemArray) // automatically marks value update-needed
        Copy(arrayNewValue localArray)  // does NOT automatically mark value update-needed; not a dataItem
        CopyAndReset(latchedBoolean latchedBooleanDestination false)  // automatically marks value update-needed
        MySubVI()
    )
)))


define(MySubVI dv(.VirtualInstrument (
    Params: c(
        o(.Double subVIOutputParameter)
    )
    Locals: c(
       de(dv(.Int32 42) subVILocalDataItem)
    )
    clump(1
        Copy(321.0 subVIOutputParameter) // does NOT automatically mark value update-needed; subVI dataItems ignored
        Copy(789 subVILocalDataItem) // does NOT automatically mark value update-needed; subVI dataItems ignored
    )
)))

enqueue (MyVI)
